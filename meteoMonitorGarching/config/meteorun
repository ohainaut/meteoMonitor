#! /bin/csh
# 
#.file		meteorun_garching
#.purpose	calls the meteoli MeteoMonitor program, 
#		converts the result in appropriate format, and
#		loops forever
#.usage	
#		Debug:  		meteorun
#		Real operation:   	meteorun > /dev/null &
#.version 	Nov97 OR.Hainaut
#
# Modification  FG 02/oct/2000 The new asm machine for La Silla is wlsasm.ls.eso.org

#- definitions:

set asmWs = wlsasm.ls.eso.org

#- Initializations:

@ i = 1

set path=(  .  ..  /usr/local/bin  $path)

/bin/cp dimm.dat dimm.bup

@ lastStored = 0

while(1)
  echo "--$i--------------"
  date

  #-- get the last data
  echo "ftp"

  ftp -n $asmWs <<EOF 
user anonymous meteomonitor@lasilla.eso.org
cd /pub
get meteo.last meteo.last
get dimm.last  dimm.last
get photonight.lis
quit
EOF


  echo "dbg: dimm.dat:"
  cat dimm.last
  echo "dbg: read_dimm"
  read_dimm

  /bin/mv dimm_new.dat dimm.dat
  /bin/rm -f ntt.last dimm.last

  echo "dbg: check UT" 
  date -u +"%H%M" >! ut.last

  ln -s current.gif current_`cat ut.last`.gif
  ln -s current_zoom.gif current_zoom_`cat ut.last`.gif
  ln -s xearth.gif xearth_`cat ut.last`.gif


#-- create the new page and graphs.
  echo "meteoli"
  meteoli

#-- converts the PS into GIFs

   echo "convert ps to gif:"
   echo "   current.ps"
   echo '(current.ps)run' | gs -q -dNOPAUSE -dNO_PAUSE -sDEVICE=ppmraw -dTextAlphaBits=4 -sOutputFile=current.ppm
   pnmcrop < current.ppm | ppmquant 256 | ppmtogif > currentTmp.gif
   mogrify -rotate 90 currentTmp.gif
   mv currentTmp.gif current.gif

   echo "   current_zoom.ps"
   echo '(current_zoom.ps)run' | gs -q -dNOPAUSE -dNO_PAUSE -sDEVICE=ppmraw -dTextAlphaBits=4 -sOutputFile=current_zoom.ppm
   pnmcrop < current_zoom.ppm | ppmquant 256 | ppmtogif > current_zoomTmp.gif
   mogrify -rotate 90 current_zoomTmp.gif
   mv current_zoomTmp.gif current_zoom.gif

#-- create the earth map
  echo "xearth"
  xearth -proj merc -pos fixed,0,0 -gif -size 320,150 -nostars -night 35 -ncolors 16 -term 100 >! xearth.gif


#-- housekeeping: delete old files
   @ i = `/bin/ls -1 *gif | wc -l`

   if ( $i > 18 ) then
      @ i = $i - 18
      /bin/rm `/bin/ls -1t {current,xearth}*gif | /bin/tail -$i`
   endif

  /bin/rm -f ps_* >& /dev/null

#-- store ps in archive
  if ( `date -u +%d` != lastStored )  then
    if ( `date -u +%H` == 10 ) then
       /bin/cp meteomonitor.ps Archive/end`date -u +%y%m%d`.ps
       @ lastStored =  `date -u +%d`
    endif
 endif


#-- wait for next iteration
  echo "done, sleeping"
  sleep 81
  @ i ++
end
